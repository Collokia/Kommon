description = 'Kommon'

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

if (rootProject != project) {
    configure(rootProject) {
        apply plugin: 'idea'
    }
}

subprojects { sub ->
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'kotlin'
    apply plugin: 'pride'

    if (sub.name == "vert.k") {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    else {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    /*
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from kdoc.destinationDir
    }
    javadocJar.dependsOn kdoc
       */

    build.dependsOn sourceJar // , javadocJar

    //noinspection GroovyAssignabilityCheck
    dependencies {
        compile "com.google.guava:guava:$version_guava"

        provided "org.slf4j:slf4j-api:$version_slf4j"

        testRuntime "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testRuntime "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        testCompile "org.slf4j:slf4j-api:$version_slf4j"

        testCompile "junit:junit:$version_junit"
        testCompile "com.google.truth:truth:$version_truth"

        testRuntime "ch.qos.logback:logback-classic:$version_logback"
        testRuntime "ch.qos.logback:logback-access:$version_logback"
        testRuntime "org.slf4j:jcl-over-slf4j:$version_slf4j"
        testRuntime "org.slf4j:jul-to-slf4j:$version_slf4j"
        testRuntime "org.slf4j:log4j-over-slf4j:$version_slf4j"
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://collokia.artifactoryonline.com/collokia/collokia-oss'
        }
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {url 'https://oss.sonatype.org/content/groups/staging'    }
    }

    //noinspection GroovyAssignabilityCheck
    configurations.all {
        exclude group: "org.slf4j", module: "slf4j-simple"
        exclude group: "commons-logging"
        exclude group: "log4j"
        exclude group: "com.google.code.findbugs"
        exclude group: "org.slf4j", module: "slf4j-log4j12"

        resolutionStrategy {
            cacheDynamicVersionsFor 1, 'days'  // things with wildcard version numbers
            cacheChangingModulesFor 1, 'hours'  // things with same version, but content of module changes, i.e. SNAPSHOT
        }
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            //    artifact javadocJar {
            //        classifier "javadoc"
            //    }
            }
        }
        repositories {
            maven {
                url "https://collokia.artifactoryonline.com/collokia/collokia-oss"
                credentials {
                    username "${collokiaRepoUser}"
                    password "${collokiaRepoPass}"
                }
            }
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://collokia.artifactoryonline.com/collokia/collokia-oss'
        }
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'http://repo.spring.io/plugins-release'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.springframework.build.gradle:propdeps-plugin:$propdepsVersion"
        classpath "com.prezi.pride:gradle-pride-plugin:$prideVersion"
    }
}

